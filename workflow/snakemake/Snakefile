# Note that if you are working on the analysis development locally, i.e. outside
# of the REANA platform, you can proceed as follows:
#
#   $ mkdir snakemake-local-run
#   $ cd snakemake-local-run
#   $ virtualenv ~/.virtualenvs/root6-roofit-snakemake
#   $ source ~/.virtualenvs/root6-roofit-snakemake/bin/activate
#   $ pip install snakemake
#   $ cp -a ../code .
#   $ snakemake -s ../workflow/snakemake/Snakefile \
#               --configfile ../workflow/snakemake/inputs.yaml -p --cores 1
#   $ open results/plot.png

rule all:
    input:
        "results/data.root",
        "results/plot.png"
        

rule gendata:
    input:
        gendata_tool=config["gendata"]
    output:
        "results/data.root"
    params:
        events=config["events"],
        data=config["data"]
    container:
        "reanahub/reana-env-root6:6.18.04"
    shell:
        "mkdir -p results && root -b -q '{input.gendata_tool}({params.events},\"{params.data}\")'"

rule fitdata:
    input:
        fitdata_tool=config["fitdata"]
    output:
        "results/plot.png"
    container:
        "reanahub/reana-env-root6:6.18.04"
    params:
        data=config["data"],
        plot=config["plot"]
    shell: 
        "root -b -q '{input.fitdata_tool}(\"{params.data}\",\"{params.plot}\")'"
